#!/usr/bin/perl

use FindBin;
use lib $FindBin::Bin;

use strict;
use warnings FATAL => 'all';

use File::Basename;
use File::Temp;
use _kxLab;

my $distro = _kxLab::distro_name();

# Global variables
my $header_printed = 0;

my $cleanup = $ENV{DO_CREATE_DIST_FILES} ? 0 : 1;
my ($tempfd, $tempname);

# cleanpath( path )
sub cleanpath
{
  my $path = shift;
  $path =~ s!/{2,}!/!g;
  $path =~ s!^.*dist/!!;
  return $path;
}

# rootfs( rootfs_dest_dir, pkg )
sub rootfs
{
  my $dest_dir = cleanpath( shift );
  my $file = basename( shift, ".tgz" );
  print $tempfd "$dest_dir/var/log/$distro/packages/$file\n";
}

# install( installpkg, rootfs_dest_dir, verbose, targets )
sub install
{
  my $installpkg = shift;
  my $rootfs_dest_dir = shift;
  my $verbose = shift;
  my $targets = shift;

  foreach my $target ( @{$targets} )
  {
    my $pkg  = basename( $target );
    my $cdir = dirname( $target );
    if( !$header_printed )
    {
      print "\n======= Installing packages =======\n";
      $header_printed = 1;
    }
    print "Installing $target ...\n" if ( $verbose );
    _kxLab::system( "cd $cdir; $installpkg --root $rootfs_dest_dir $pkg" );
    rootfs( $rootfs_dest_dir, $target );
  }
}

my $hardware;
my $rootfs_dest_dir;
my @targets;
my $verbose    = $ENV{VERBOSE};
my $curdir     = $ENV{CWD};
my $installpkg = $ENV{INSTALL_PACKAGE};
my $fname      = "";
my $dest_fname = "";

foreach ( @ARGV )
{
  push @targets, $_;
}
$hardware = pop @targets;
$rootfs_dest_dir = pop @targets;

if ( $curdir )
{
  $fname = "$curdir/.$hardware.rootfs.XXXXXX";
  $dest_fname = "$curdir/.$hardware.rootfs";
}
else
{
  $fname = ".$hardware.rootfs.XXXXXX";
  $dest_fname = ".$hardware.rootfs";
}

($tempfd, $tempname) = File::Temp::tempfile( $fname, UNLINK => $cleanup );

install( $installpkg, $rootfs_dest_dir, $verbose, \@targets );

# reverse file line by line:
_kxLab::system( "tac $tempname >> $dest_fname" );
_kxLab::system( "rm -f $tempname" );
